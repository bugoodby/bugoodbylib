property remoteMac : "eppc://test:test@172.16.174.128"

on execPrint(aSetting, aSavePath)
	
	using terms from application "Finder"
		tell application "Finder" of machine remoteMac
			open ("/Applications/Preview.app" as POSIX file as alias)
			open ("/System/Library/CoreServices/System Events.app" as POSIX file as alias)
		end tell
	end using terms from
	
	tell application "Preview" of machine remoteMac
		activate
		open "/Users/test/Documents/share/s_A4_P_1p.pdf"
	end tell
	delay 1
	
	using terms from application "System Events"
		tell application "System Events" of machine remoteMac
			tell process "Preview"
				set frontmost to true
				keystroke "p" using {command down}
				---------------------------------------------
				tell sheet 1 of window 1
					click menu item (printer of aSetting) of menu 1 of (click pop up button 1)
					delay 0.5
					click menu item (psize of aSetting) of menu 1 of (click pop up button 3)
					if (ori of aSetting) is "L" then
						click radio button 2 of radio group 2
					end if
					click menu item "Finishing Options" of menu 1 of (click pop up button 4)
					delay 0.5
					click menu item (staple of aSetting) of menu 1 of (click pop up button 5)
					if (punch of aSetting) is "ON" then
						tell checkbox "Punch" to if not (its value as boolean) then click
					end if
					--click menu item "Letter" of menu 1 of (click pop up button 7)
					click button "Print"
				end tell
				---------------------------------------------
				delay 10
			end tell
		end tell
	end using terms from
	
	tell application "Preview" of machine remoteMac
		close document 1
		quit
	end tell
	
	delay 5
	tell application "Preview"
		set aName to (tag of aSetting) & ".pdf"
		if aName is "" then tell document 1 to set aName to name
		set newPath to aSavePath & "/" & aName
		--display dialog newPath
		save document 1 in POSIX file newPath
		close document 1
	end tell
    
	return true
end execPrint

on run argv
	set aSavePath to (path to documents folder)'s POSIX path & "hogehogetest"
	-- display dialog aSavePath
	do shell script "mkdir -p " & aSavePath
    
	set aSetting to {tag:"", dataPath:"", printer:"", psize:"", ori:"P", staple:"", punch:"false"}
	if (count of argv) = 0 then
		copy "/Users/test/Documents/share/s_A4_P_1p.pdf" to dataPath of aSetting
		copy "Simulated Color Laser @ MBA" to printer of aSetting
		copy "A4" to psize of aSetting
		copy "P" to ori of aSetting
		copy "1 Staple" to staple of aSetting
		copy "ON" to punch of aSetting
	else
		copy (item 1 of argv) to tag of aSetting
		copy (item 2 of argv) to dataPath of aSetting
		copy (item 3 of argv) to printer of aSetting
		copy (item 4 of argv) to psize of aSetting
		copy (item 5 of argv) to ori of aSetting
		copy (item 6 of argv) to staple of aSetting
		copy (item 7 of argv) to punch of aSetting
		log aSetting
	end if
	
	set ret to my execPrint(aSetting, aSavePath)
	return ret
end run
