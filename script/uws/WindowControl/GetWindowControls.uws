//
// 指定したウィンドウのコントロール一覧を出力する
//
STOPFORM(TRUE)

seq = 1

//winName = INPUT("ウィンドウタイトルを入力してください")
//wid = GETID(winName)

While 1
	FUKIDASI("Please ALT + Click for Window Information Capturing.")
	Ifb GETKEYSTATE(VK_ALT) And GETKEYSTATE(VK_LBUTTON) Then 
		FUKIDASI("Getting Window Information ...")
		wid = GETID(GET_FROMPOINT_WIN)
		
		fp = FOPEN("result_GetWindowControls-" + seq + ".csv", F_WRITE)

		FPUT(fp, "WindowID,Handle,ST_TITLE,ST_CLASS,ST_X,ST_Y,ST_WIDTH,ST_HEIGHT,ST_CLX,ST_CLY,ST_CLWIDTH,ST_CLHEIGHT,ST_PARENT,ST_PATH,ST_PROCESS,ST_WIN64")
		line = GetWindowInfoFromID(wid)
		FPUT(fp, line)

		OutputControls(fp, wid, ITM_BTN, "ITM_BTN")
		OutputControls(fp, wid, ITM_LIST, "ITM_LIST")
		OutputControls(fp, wid, ITM_TAB, "ITM_TAB")
		OutputControls(fp, wid, ITM_MENU, "ITM_MENU")
		OutputControls(fp, wid, ITM_TREEVEW, "ITM_TREEVEW")
		OutputControls(fp, wid, ITM_LSTVEW, "ITM_LSTVEW")
		OutputControls(fp, wid, ITM_EDIT, "ITM_EDIT")
		OutputControls(fp, wid, ITM_STATIC, "ITM_STATIC")
		OutputControls(fp, wid, ITM_STATUSBAR, "ITM_STATUSBAR")
		OutputControls(fp, wid, ITM_TOOLBAR, "ITM_TOOLBAR")
//		OutputControls(fp, wid, ITM_ACCCLK, "ITM_ACCCLK")
//		OutputControls(fp, wid, ITM_ACCTXT, "ITM_ACCTXT")

		FCLOSE(fp)
		seq = seq + 1
	Else
		SLEEP(0.5)
	Endif
Wend



FUNCTION GetWindowInfoFromID( id )
	line = ""
	line = line + id + ","							// ウィンドウID
	line = line + IDTOHND(id) + ","					// ウィンドウハンドル
	line = line + STATUS(id, ST_TITLE) + ","		// タイトル
	line = line + STATUS(id, ST_CLASS) + ","		// クラス
	line = line + STATUS(id, ST_X) + ","			// X座標
	line = line + STATUS(id, ST_Y) + ","			// Y座標
	line = line + STATUS(id, ST_WIDTH) + ","		// 幅
	line = line + STATUS(id, ST_HEIGHT) + ","		// 高さ
	line = line + STATUS(id, ST_CLX) + ","			// クライアント領域のX座標
	line = line + STATUS(id, ST_CLY) + ","			// クライアント領域のY座標
	line = line + STATUS(id, ST_CLWIDTH) + ","		// クライアント領域の幅
	line = line + STATUS(id, ST_CLHEIGHT) + ","		// クライアント領域の高さ
	line = line + STATUS(id, ST_PARENT) + ","		// 親ウィンドウのID
	line = line + STATUS(id, ST_PATH) + ","			// EXEのパス
	line = line + STATUS(id, ST_PROCESS) + ","		// プロセスID
	line = line + STATUS(id, ST_WIN64) + ","		// 64bitアプリか
	
	RESULT = line
FEND


FUNCTION OutputControls( fp, id, type, typestr )
	FPUT(fp, typestr)
	itemNum = GetItem(id, type, -1)
	For i = 0 to itemNum - 1
		ctlName = ALL_ITEM_LIST[i]
		//ctlhnd = GETCTLHND(id, ctlName)
		line = ",[" + i + "]," + ctlName
		FPUT(fp, line)
	Next
	
	RESULT = True
FEND

