//
// 指定したウィンドウのコントロール一覧を出力する
//
STOPFORM(TRUE)

seq = 1

FUKIDASI("Please ALT + Click for Window Information Capturing.")
While 1
	Ifb GETKEYSTATE(VK_ALT) And GETKEYSTATE(VK_LBUTTON) Then 
		FUKIDASI("Getting Window Information ...")
		wid = GETID(GET_FROMPOINT_WIN)
		wname = STATUS(wid, ST_TITLE)
		
		fp = FOPEN("WindowInfo-" + seq + ".txt", F_WRITE)
		seq = seq + 1
		
		OutputWindowInfoFromID(fp, wid)
		OutputControlsAutoJudge(fp, wid, ITM_BTN, "ITM_BTN")
		OutputControlsAutoJudge(fp, wid, ITM_LIST, "ITM_LIST")
		OutputControlsAutoJudge(fp, wid, ITM_TAB, "ITM_TAB")
		OutputControlsAutoJudge(fp, wid, ITM_MENU, "ITM_MENU")
		OutputControlsAutoJudge(fp, wid, ITM_TREEVEW, "ITM_TREEVEW")
		OutputControlsAutoJudge(fp, wid, ITM_LSTVEW, "ITM_LSTVEW")
		OutputControlsAutoJudge(fp, wid, ITM_EDIT, "ITM_EDIT")
		OutputControlsAutoJudge(fp, wid, ITM_STATIC, "ITM_STATIC")
		OutputControlsAutoJudge(fp, wid, ITM_STATUSBAR, "ITM_STATUSBAR")
		OutputControlsAutoJudge(fp, wid, ITM_TOOLBAR, "ITM_TOOLBAR")
		//OutputControlsAutoJudge(fp, wid, ITM_ACCCLK, "ITM_ACCCLK")
		//OutputControlsAutoJudge(fp, wid, ITM_ACCTXT, "ITM_ACCTXT")

		FCLOSE(fp)
		FUKIDASI("Got Window Information [" + wname + "]")
	Else
		SLEEP(0.5)
	Endif
Wend


FUNCTION OutputWindowInfoFromID( fp, id )
	FPUT(fp, "### Window Informatin")
	FPUT(fp, "  ID          : " + id)
	FPUT(fp, "  Handle      : " + IDTOHND(id))
	FPUT(fp, "  ST_TITLE    : " + STATUS(id, ST_TITLE))
	FPUT(fp, "  ST_CLASS    : " + STATUS(id, ST_CLASS))
	FPUT(fp, "  ST_X        : " + STATUS(id, ST_X))
	FPUT(fp, "  ST_Y        : " + STATUS(id, ST_Y))
	FPUT(fp, "  ST_WIDTH    : " + STATUS(id, ST_WIDTH))
	FPUT(fp, "  ST_HEIGHT   : " + STATUS(id, ST_HEIGHT))
	FPUT(fp, "  ST_CLX      : " + STATUS(id, ST_CLX))
	FPUT(fp, "  ST_CLY      : " + STATUS(id, ST_CLY))
	FPUT(fp, "  ST_CLWIDTH  : " + STATUS(id, ST_CLWIDTH))
	FPUT(fp, "  ST_CLHEIGHT : " + STATUS(id, ST_CLHEIGHT))
	FPUT(fp, "  ST_PARENT   : " + STATUS(id, ST_PARENT))
	FPUT(fp, "  ST_PATH     : " + STATUS(id, ST_PATH))
	FPUT(fp, "  ST_PROCESS  : " + STATUS(id, ST_PROCESS))
	FPUT(fp, "  ST_WIN64    : " + STATUS(id, ST_WIN64))
	FPUT(fp, "")
	
	RESULT = True
FEND

FUNCTION OutputControlsAutoJudge( fp, id, type, typestr )
	itemNum = GetItem(id, type, -1)
	itemNum2 = GetItem(id, type, 1)
	
	Ifb itemNum = itemNum2 Then
		OutputControls(fp, id, type, typestr)
	Else
		OutputControlsForList(fp, id, type, typestr)
	Endif
	
	RESULT = True
FEND

FUNCTION OutputControls( fp, id, type, typestr )
	itemNum = GetItem(id, type, -1)
	FPUT(fp, "### " + typestr + "(" + itemNum + ")")
	
	For i = 0 to itemNum - 1
		ctlName = ALL_ITEM_LIST[i]
		ctlhnd = GETCTLHND(id, ctlName)
		line = "  [" + i + "] " + ctlName + ", " + ctlhnd
		FPUT(fp, line)
	Next
	FPUT(fp, "")
	
	RESULT = True
FEND

FUNCTION OutputControlsForList( fp, id, type, typestr )
	itemNum = GetItem(id, type, -1)
	FPUT(fp, "### " + typestr + "(" + itemNum + ")")
	
	For i = 1 to itemNum
		ctlNum = GetItem(id, type, i)
		For j = 0 to ctlNum - 1
			ctlName = ALL_ITEM_LIST[j]
			ctlhnd = GETCTLHND(id, ctlName)
			line = "  [" + i + "][" + j + "] " + ctlName + ", " + ctlhnd
			FPUT(fp, line)
		Next
	Next
	FPUT(fp, "")
	
	RESULT = True
FEND

